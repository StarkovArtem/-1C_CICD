#!/usr/bin/executor

#требуется ILogging.sbsl
#требуется oc_main.sbsl
#требуется oc_batch_mode.sbsl
#требуется oc_ibsrv.sbsl

// ПрограмнныйИнтерфейс

@Глобально
метод СоздатьИнформационнуюБазу(СтрокаДоступа: Строка, ПутьКФайламПлатформы: Строка, РежимИсполнения: oc_main.РежимыИсполнения)

    знч ВремяИсполненияОпераций = новый ILogging.ВремяИсполненияОпераций()
    Консоль.Записать("\нCreateInfobase:")

    знч ИспользуетсяАвтономныйСервер = РежимИсполнения == oc_main.РежимыИсполнения.АвтономныйСервер
    ВремяИсполненияОпераций.НачатьЗамер("СоздатьИнформационнуюБазу")

    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСервернаяИБ(СтрокаДоступа)
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)
        знч ПутьКИнформационнойБазе = oc_main.ПреобразоватьВПутьКИнформационнойБазе(СтрокаДоступа)
        oc_ibsrv.СоздатьИнформационнуюБазу(
            ПутьКИнформационнойБазе,
            ИсполняемыйФайлПлатформы)
    иначе
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(СтрокаДоступа)
        oc_batch_mode.СоздатьИнформационнуюБазу(
            СтрокаСоединения,
            ИсполняемыйФайлПлатформы)
    ;

    ВремяИсполненияОпераций.ЗавершитьЗамер("СоздатьИнформационнуюБазу")
;

@Глобально
метод ЗагрузитьКонфигурацию(ПутьКФайлуСборки: Строка, СтрокаДоступа: Строка, ПутьКФайламПлатформы: Строка, РежимИсполнения: oc_main.РежимыИсполнения,
            ИмяПользователя: Строка = "", Пароль: Строка = "", ИмяРасширения: Строка = "")

    знч ВремяИсполненияОпераций = новый ILogging.ВремяИсполненияОпераций()
    Консоль.Записать("\нLoadFromFile:")

    знч ИспользуетсяАвтономныйСервер = РежимИсполнения == oc_main.РежимыИсполнения.АвтономныйСервер
    ВремяИсполненияОпераций.НачатьЗамер("ЗагрузитьКонфигурацию")

    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСервернаяИБ(СтрокаДоступа)
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)
        знч ПутьКИнформационнойБазе = oc_main.ПреобразоватьВПутьКИнформационнойБазе(СтрокаДоступа)
        oc_ibsrv.ЗагрузитьКонфигурацию(
            ПутьКФайлуСборки,
            ПутьКИнформационнойБазе,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения)
    иначе
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(СтрокаДоступа)
        oc_batch_mode.ЗагрузитьКонфигурацию(
            ПутьКФайлуСборки,
            СтрокаСоединения,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения)
    ;

    ВремяИсполненияОпераций.ЗавершитьЗамер("ЗагрузитьКонфигурацию")
;

@Глобально
метод ЗагрузитьФайлыКонфигурации(ПутьКФайлуСборки: Строка, СтрокаДоступа: Строка, ПутьКФайламПлатформы: Строка, РежимИсполнения: oc_main.РежимыИсполнения,
            ИмяПользователя: Строка = "", Пароль: Строка = "", ИмяРасширения: Строка = "", ФайлыДляЗагрузки: Файл|Неопределено = Неопределено)

    знч ВремяИсполненияОпераций = новый ILogging.ВремяИсполненияОпераций()
    Консоль.Записать("\нLoadFromFiles:")

    знч ИспользуетсяАвтономныйСервер = РежимИсполнения == oc_main.РежимыИсполнения.АвтономныйСервер
    знч ПутьКФайлуЗагрузки = (ФайлыДляЗагрузки == Неопределено) ? "" : ФайлыДляЗагрузки.Путь
    ВремяИсполненияОпераций.НачатьЗамер("ЗагрузитьФайлыКонфигурации")

    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСервернаяИБ(СтрокаДоступа)
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)
        знч ПутьКИнформационнойБазе = oc_main.ПреобразоватьВПутьКИнформационнойБазе(СтрокаДоступа)
        oc_ibsrv.ЗагрузитьФайлыКонфигурации(
            ПутьКФайлуСборки,
            ПутьКИнформационнойБазе,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения,
            ПутьКФайлуЗагрузки)
    иначе
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(СтрокаДоступа)
        oc_batch_mode.ЗагрузитьФайлыКонфигурации(
            ПутьКФайлуСборки,
            СтрокаСоединения,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения,
            ПутьКФайлуЗагрузки)
    ;

    ВремяИсполненияОпераций.ЗавершитьЗамер("ЗагрузитьФайлыКонфигурации")
;

@Глобально
метод ВыгрузитьКонфигурацию(ПутьКФайлуСборки: Строка, СтрокаДоступа: Строка, ПутьКФайламПлатформы: Строка, РежимИсполнения: oc_main.РежимыИсполнения,
            ИмяПользователя: Строка = "", Пароль: Строка = "", ИмяРасширения: Строка = "")

    знч ВремяИсполненияОпераций = новый ILogging.ВремяИсполненияОпераций()
    Консоль.Записать("\нDump:")

    знч ИспользуетсяАвтономныйСервер = РежимИсполнения == oc_main.РежимыИсполнения.АвтономныйСервер
    ВремяИсполненияОпераций.НачатьЗамер("ВыгрузитьКонфигурацию")

    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСервернаяИБ(СтрокаДоступа)
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)
        знч ПутьКИнформационнойБазе = oc_main.ПреобразоватьВПутьКИнформационнойБазе(СтрокаДоступа)
        oc_ibsrv.ВыгрузитьКонфигурацию(
            ПутьКФайлуСборки,
            ПутьКИнформационнойБазе,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения)
    иначе
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(СтрокаДоступа)
        oc_batch_mode.ВыгрузитьКонфигурацию(
            ПутьКФайлуСборки,
            СтрокаСоединения,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения)
    ;

    ВремяИсполненияОпераций.ЗавершитьЗамер("ВыгрузитьКонфигурацию")
;

@Глобально
метод ВыгрузитьФайлыКонфигурации(ПутьКФайлуСборки: Строка, СтрокаДоступа: Строка, ПутьКФайламПлатформы: Строка, РежимИсполнения: oc_main.РежимыИсполнения,
            ИмяПользователя: Строка = "", Пароль: Строка = "", ИмяРасширения: Строка, ФайлыДляВыгрузки: Массив<Строка>|Строка|Файл = "", ПутьКФайлуВерсии = "")

    пер ПутьФайлВыгрузки = ""
    пер ТолькоDumpConfig = Ложь
    если ФайлыДляВыгрузки.ПолучитьТип() == Тип<Файл>
        ПутьФайлВыгрузки = (ФайлыДляВыгрузки как Файл).Путь
    иначе
        знч НастройкиВыгрузки = ПолучитьНастройкиВыгрузки(ПутьКФайлуСборки, ФайлыДляВыгрузки как Массив<Строка>|Строка)
        ПутьФайлВыгрузки = НастройкиВыгрузки.Получить("ПутьФайлВыгрузки") как Строка
        ТолькоDumpConfig = НастройкиВыгрузки.Получить("ВыгружатьDumpConfig") как Булево
    ;

    знч ВремяИсполненияОпераций = новый ILogging.ВремяИсполненияОпераций()
    если ТолькоDumpConfig
        Консоль.Записать("\нDumpFiles(Config dump info):")
    иначе
        Консоль.Записать("\нDumpFiles:")
    ;

    знч ИспользуетсяАвтономныйСервер = РежимИсполнения == oc_main.РежимыИсполнения.АвтономныйСервер
    ВремяИсполненияОпераций.НачатьЗамер("ВыгрузитьФайлыКонфигурации")

    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСервернаяИБ(СтрокаДоступа)

        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)
        знч ПутьКИнформационнойБазе = oc_main.ПреобразоватьВПутьКИнформационнойБазе(СтрокаДоступа)

        если ТолькоDumpConfig

            oc_ibsrv.ВыгрузитьФайлыКонфигурации(
                ПутьКФайлуСборки,
                ПутьКИнформационнойБазе,
                ИсполняемыйФайлПлатформы,
                ИмяПользователя,
                Пароль,
                ИмяРасширения,
                ТолькоDumpConfig)
        иначе

            oc_ibsrv.ВыгрузитьФайлыКонфигурации(
                ПутьКФайлуСборки,
                ПутьКИнформационнойБазе,
                ИсполняемыйФайлПлатформы,
                ИмяПользователя,
                Пароль,
                ИмяРасширения,
                Ложь,
                ПутьКФайлуВерсии)
        ;

    иначе

        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(СтрокаДоступа)

        если ТолькоDumpConfig

            oc_batch_mode.ВыгрузитьФайлыКонфигурации(
                ПутьКФайлуСборки,
                СтрокаСоединения,
                ИсполняемыйФайлПлатформы,
                ИмяПользователя,
                Пароль,
                ИмяРасширения,
                ТолькоDumpConfig,
                "")
        иначе

            oc_batch_mode.ВыгрузитьФайлыКонфигурации(
                ПутьКФайлуСборки,
                СтрокаСоединения,
                ИсполняемыйФайлПлатформы,
                ИмяПользователя,
                Пароль,
                ИмяРасширения,
                Ложь,
                ПутьФайлВыгрузки,
                ПутьКФайлуВерсии)
        ;
    ;

    ВремяИсполненияОпераций.ЗавершитьЗамер("ВыгрузитьФайлыКонфигурации")
;

@Глобально
метод СравнитьКонфигурации(ПутьКФайлуОтчета: Строка, СтрокаДоступа: Строка, ПутьКФайламПлатформы: Строка,
                    ИмяПользователя: Строка = "", Пароль: Строка = "", РежимСравнения: oc_main.РежимыСравнения, ИмяКонфигурацииПоставщика = "")

    знч ВремяИсполненияОпераций = новый ILogging.ВремяИсполненияОпераций()
    Консоль.Записать("\нCompareCfg:")

    знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
    ВремяИсполненияОпераций.НачатьЗамер("СравнитьКонфигурации")

    знч ПерваяКонфигурация = "MainConfiguration"
    знч ВтораяКонфигурация = (РежимСравнения == oc_main.РежимыСравнения.СравнитьСКонфигурациейИБ)
        ? "DBConfiguration"
        : "VendorConfiguration"

    знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(СтрокаДоступа)
    oc_batch_mode.СравнитьКонфигурации(ПутьКФайлуОтчета,
        СтрокаСоединения,
        ИсполняемыйФайлПлатформы,
        ИмяПользователя,
        Пароль,
        ПерваяКонфигурация,
        ВтораяКонфигурация,
        ИмяКонфигурацииПоставщика)

    ВремяИсполненияОпераций.ЗавершитьЗамер("СравнитьКонфигурации")
;

// СлужеюныеПроцедурыИФункции

@Локально
метод ПолучитьНастройкиВыгрузки(ПутьКФайлуСборки: Строка, ФайлыКонфигурации: Массив<Строка>|Строка) : Соответствие<Строка, Строка|Булево>

    знч СР = Файлы.СимволРазделителя
    пер ФайлыВыгрузки = новый Массив<Строка>()

    если ФайлыКонфигурации.ПолучитьТип() == Тип<Строка>
        ФайлыВыгрузки = (ФайлыКонфигурации как Строка).Разделить(",", Ложь)
    иначе если ФайлыВыгрузки.ПолучитьТип() == Тип<Массив<Строка>>
        ФайлыВыгрузки = ФайлыКонфигурации как Массив<Строка>
    ;

    пер ТекстФайлаВыгрузки = ""
    пер ВыгружатьDumpConfig = Ложь

    для ПутьКФайлу из ФайлыВыгрузки
        если ПутьКФайлу.ЗаканчиваетсяНа("ConfigDumpInfo.xml")
            ВыгружатьDumpConfig = Истина
            продолжить
        ;
        ТекстФайлаВыгрузки = ТекстФайлаВыгрузки + ПутьКФайлу + Символы.НОВАЯ_СТРОКА
    ;

    знч Результат = новый Соответствие<Строка, Строка|Булево>()
    
    пер ПутьФайлВыгрузки = ""
    если не ТекстФайлаВыгрузки.Пусто()
        ПутьФайлВыгрузки = ПутьКФайлуСборки + СР + "files_out"
        (новый Файл(ПутьФайлВыгрузки)).ОткрытьПотокЗаписи().Записать(ТекстФайлаВыгрузки).Закрыть()
    ;

    Результат.Вставить("ПутьФайлВыгрузки", ПутьФайлВыгрузки)
    Результат.Вставить("ВыгружатьDumpConfig", ВыгружатьDumpConfig)

    возврат Результат
;