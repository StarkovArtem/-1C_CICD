#!/usr/bin/executor

#требуется oc_main.sbsl
#требуется oc_batch_mode.sbsl
#требуется oc_ibsrv.sbsl

@Глобально
метод СоздатьИнформационнуюБазу(ПутьКИнформационнойБазе: Строка, ПутьКФайламПлатформы: Строка, ИспользуетсяАвтономныйСервер: Булево)

    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСтрокаСоединения(ПутьКИнформационнойБазе)
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)
        oc_ibsrv.СоздатьИнформационнуюБазу(ПутьКИнформационнойБазе,
            ИсполняемыйФайлПлатформы)
    иначе
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(ПутьКИнформационнойБазе)
        oc_batch_mode.СоздатьИнформационнуюБазу(СтрокаСоединения,
            ИсполняемыйФайлПлатформы)
    ;
;

@Глобально
метод ЗагрузитьКонфигурацию(ПутьКФайлуСборки: Строка, ПутьКИнформационнойБазе: Строка, ПутьКФайламПлатформы: Строка, ИспользуетсяАвтономныйСервер: Булево,
            ИмяПользователя: Строка = "", Пароль: Строка = "", ИмяРасширения: Строка = "")

    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСтрокаСоединения(ПутьКИнформационнойБазе)
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)
        oc_ibsrv.ЗагрузитьКонфигурацию(ПутьКФайлуСборки,
                ПутьКИнформационнойБазе,
                ИсполняемыйФайлПлатформы,
                ИмяПользователя,
                Пароль,
                ИмяРасширения)
    иначе
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(ПутьКИнформационнойБазе)
        oc_batch_mode.ЗагрузитьКонфигурацию(ПутьКФайлуСборки,
            СтрокаСоединения,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения)
    ;
;

@Глобально
метод ЗагрузитьФайлыКонфигурации(ПутьКФайлуСборки: Строка, ПутьКИнформационнойБазе: Строка, ПутьКФайламПлатформы: Строка, ИспользуетсяАвтономныйСервер: Булево,
            ИмяПользователя: Строка = "", Пароль: Строка = "", ИмяРасширения: Строка = "", ФайлыДляЗагрузки: Файл|Неопределено = Неопределено)

    знч ПутьКФайлуЗагрузки = (ФайлыДляЗагрузки == Неопределено) ? "" : ФайлыДляЗагрузки.Путь
    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСтрокаСоединения(ПутьКИнформационнойБазе)
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)
        oc_ibsrv.ЗагрузитьФайлыКонфигурации(
            ПутьКФайлуСборки,
            ПутьКИнформационнойБазе,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения,
            ПутьКФайлуЗагрузки)
    иначе
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(ПутьКИнформационнойБазе)
        oc_batch_mode.ЗагрузитьФайлыКонфигурации(
            ПутьКФайлуСборки,
            СтрокаСоединения,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения,
            ПутьКФайлуЗагрузки)
    ;
;

@Глобально
метод ВыгрузитьКонфигурацию(ПутьКФайлуСборки: Строка, ПутьКИнформационнойБазе: Строка, ПутьКФайламПлатформы: Строка, ИспользуетсяАвтономныйСервер: Булево,
            ИмяПользователя: Строка = "", Пароль: Строка = "", ИмяРасширения: Строка = "")

    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСтрокаСоединения(ПутьКИнформационнойБазе)
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)
        oc_ibsrv.ВыгрузитьКонфигурацию(
                ПутьКФайлуСборки,
                ПутьКИнформационнойБазе,
                ИсполняемыйФайлПлатформы,
                ИмяПользователя,
                Пароль,
                ИмяРасширения)
    иначе
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(ПутьКИнформационнойБазе)
        oc_batch_mode.ВыгрузитьКонфигурацию(
            ПутьКФайлуСборки,
            СтрокаСоединения,
            ИсполняемыйФайлПлатформы,
            ИмяПользователя,
            Пароль,
            ИмяРасширения)
    ;
;

@Глобально
метод ВыгрузитьФайлыКонфигурации(ПутьКФайлуСборки: Строка, ПутьКИнформационнойБазе: Строка, ПутьКФайламПлатформы: Строка, ИспользуетсяАвтономныйСервер: Булево,
            ИмяПользователя: Строка = "", Пароль: Строка = "", ИмяРасширения: Строка, ФайлыДляВыгрузки: Массив<Строка>|Строка|Файл = "", ПутьКФайлуВерсии = "")

    пер ПутьФайлВыгрузки = ""
    пер ТолькоDumpConfig = Ложь
    если ФайлыДляВыгрузки.ПолучитьТип() == Тип<Файл>
        ПутьФайлВыгрузки = (ФайлыДляВыгрузки как Файл).Путь
    иначе
        знч НастройкиВыгрузки = ПолучитьНастройкиВыгрузки(ПутьКФайлуСборки, ФайлыДляВыгрузки как Массив<Строка>|Строка)
        ПутьФайлВыгрузки = НастройкиВыгрузки.Получить("ПутьФайлВыгрузки") как Строка
        ТолькоDumpConfig = НастройкиВыгрузки.Получить("ВыгружатьDumpConfig") как Булево
    ;
    
    если ИспользуетсяАвтономныйСервер и не oc_main.ЭтоСтрокаСоединения(ПутьКИнформационнойБазе)
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Истина)

        если ТолькоDumpConfig

            oc_ibsrv.ВыгрузитьФайлыКонфигурации(
                ПутьКФайлуСборки,
                    ПутьКИнформационнойБазе,
                    ИсполняемыйФайлПлатформы,
                    ИмяПользователя,
                    Пароль,
                    ИмяРасширения,
                    ТолькоDumpConfig)
        иначе

            oc_ibsrv.ВыгрузитьФайлыКонфигурации(
                ПутьКФайлуСборки,
                    ПутьКИнформационнойБазе,
                    ИсполняемыйФайлПлатформы,
                    ИмяПользователя,
                    Пароль,
                    ИмяРасширения,
                    Ложь,
                    ПутьКФайлуВерсии)
        ;

    иначе
        знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
        знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(ПутьКИнформационнойБазе)

        если ТолькоDumpConfig

            oc_batch_mode.ВыгрузитьФайлыКонфигурации(
                ПутьКФайлуСборки,
                СтрокаСоединения,
                ИсполняемыйФайлПлатформы,
                ИмяПользователя,
                Пароль,
                ИмяРасширения,
                ТолькоDumpConfig,
                "")
        иначе

            oc_batch_mode.ВыгрузитьФайлыКонфигурации(
                ПутьКФайлуСборки,
                СтрокаСоединения,
                ИсполняемыйФайлПлатформы,
                ИмяПользователя,
                Пароль,
                ИмяРасширения,
                Ложь,
                ПутьФайлВыгрузки,
                ПутьКФайлуВерсии)
        ;
    ;
;

@Глобально
метод СравнитьКонфигурации(ПутьКФайлуОтчета: Строка, ПутьКИнформационнойБазе: Строка, ПутьКФайламПлатформы: Строка,
                    ИмяПользователя: Строка = "", Пароль: Строка = "")

    знч ИсполняемыйФайлПлатформы = oc_main.ИсполняемыйФайлПлатформы(ПутьКФайламПлатформы, Ложь)
    
    знч СтрокаСоединения = oc_main.ПреобразоватьВСтрокуСоединения(ПутьКИнформационнойБазе)
    oc_batch_mode.СравнитьКонфигурации(ПутьКФайлуОтчета,
        СтрокаСоединения,
        ИсполняемыйФайлПлатформы,
        ИмяПользователя,
        Пароль)

;

@Локально
метод ПолучитьНастройкиВыгрузки(ПутьКФайлуСборки: Строка, ФайлыКонфигурации: Массив<Строка>|Строка) : Соответствие<Строка, Строка|Булево>

    знч СР = Файлы.СимволРазделителя
    пер ФайлыВыгрузки = новый Массив<Строка>()

    если ФайлыКонфигурации.ПолучитьТип() == Тип<Строка>
        ФайлыВыгрузки = (ФайлыКонфигурации как Строка).Разделить(",", Ложь)
    иначе если ФайлыВыгрузки.ПолучитьТип() == Тип<Массив<Строка>>
        ФайлыВыгрузки = ФайлыКонфигурации как Массив<Строка>
    ;

    пер ТекстФайлаВыгрузки = ""
    пер ВыгружатьDumpConfig = Ложь

    для ПутьКФайлу из ФайлыВыгрузки
        если ПутьКФайлу.ЗаканчиваетсяНа("ConfigDumpInfo.xml")
            ВыгружатьDumpConfig = Истина
            продолжить
        ;
        ТекстФайлаВыгрузки = ТекстФайлаВыгрузки + ПутьКФайлу + Символы.НОВАЯ_СТРОКА
    ;

    знч Результат = новый Соответствие<Строка, Строка|Булево>()
    
    пер ПутьФайлВыгрузки = ""
    если не ТекстФайлаВыгрузки.Пусто()
        ПутьФайлВыгрузки = ПутьКФайлуСборки + СР + "files_out"
        (новый Файл(ПутьФайлВыгрузки)).ОткрытьПотокЗаписи().Записать(ТекстФайлаВыгрузки).Закрыть()
    ;

    Результат.Вставить("ПутьФайлВыгрузки", ПутьФайлВыгрузки)
    Результат.Вставить("ВыгружатьDumpConfig", ВыгружатьDumpConfig)

    возврат Результат
;