#!/usr/bin/executor

@Глобально
перечисление ОперационныеСистемы
    Windows,
    Linux
;

@Глобально
метод ИсполняемыйФайлПлатформы(ВерсияПлатформы: Строка, ИспользуетсяАвтономныйСервер: Булево): Строка
    
    возврат ИсполняемыйФайлПлатформыПоМаксимальнойСборке(ВерсияПлатформы, ИспользуетсяАвтономныйСервер)
;

@Глобально
метод СтрокаСоединенияДляФайловойИБ(путьКИБ: Строка): Строка

    если путьКИБ.Содержит("File=")
        или путьКИБ.Содержит("Srvr=")
        возврат путьКИБ
    ;

    возврат "\"File=\"\"%путьКИБ\"\"\""
;

@Глобально
метод ИспользуемаяОС(): ОперационныеСистемы
    знч имяОС = СредаИсполнения.ПолучитьСвойство("os.name")

    выбор когда имяОС.НачинаетсяС("windows", Истина)
        возврат ОперационныеСистемы.Windows

    когда имяОС.Содержит("nux", Истина)
        возврат ОперационныеСистемы.Linux

    иначе
        выбросить новый ИсключениеНедопустимоеСостояние("Неизвестная ОС")
    ;
;

@Локально
метод ИсполняемыйФайлПлатформыПоМаксимальнойСборке(ВерсияПлатформы: Строка, ИспользуетсяАвтономныйСервер: Булево): Строка
    
    знч СР = Файлы.СимволРазделителя
    пер ПутьКПлатформе = ""
    знч используемаяОС = ИспользуемаяОС()
    знч ИсполняемыйФайл = (ИспользуетсяАвтономныйСервер ? "ibcmd" : "1cv8")
    
    если ВерсияПлатформы.КоличествоВхождений(".") == 3
        знч установленныеПлатформы = УстановленныеПлатформы()
        если установленныеПлатформы.СодержитКлюч(ВерсияПлатформы)
            ПутьКПлатформе = установленныеПлатформы.Получить(ВерсияПлатформы)
        ;
    ;

    знч максимальныеПлатформы = МаксимальныеПлатформы()    
    если ВерсияПлатформы.КоличествоВхождений(".") == 2 и максимальныеПлатформы.СодержитКлюч(ВерсияПлатформы)
        ПутьКПлатформе = максимальныеПлатформы.Получить(ВерсияПлатформы)
    ;
    
    пер КлючиВерсий = новый Массив<Строка>(максимальныеПлатформы.Ключи())
    
    если ВерсияПлатформы.Пусто() и не КлючиВерсий.Пусто()
        КлючиВерсий = КлючиВерсий.Сортировать((строка1, строка2) -> строка2.Сравнить(строка1, Истина)) // по убыванию
        ПутьКПлатформе = максимальныеПлатформы.Получить(КлючиВерсий[0])
    ;
    
    если не ПутьКПлатформе.Пусто() и используемаяОС == ОперационныеСистемы.Windows
        возврат "%{ПутьКПлатформе}%{СР}bin%{СР}%{ИсполняемыйФайл}"
    иначе
        возврат "%{ПутьКПлатформе}%{СР}%{ИсполняемыйФайл}"
    ;
    
    знч текстОшибки = "Не найдена установленная платформа %ВерсияПлатформы" 
    выбросить новый ИсключениеНедопустимоеСостояние(текстОшибки)
    
;

@Локально
метод УстановленныеПлатформы(): Соответствие<Строка, Строка>
    пер установленныеПлатформы: Соответствие<Строка, Строка>

    знч каталогиПлатформы = КаталогиПлатформы()

    знч настройкиПоискаФайлов = новый НастройкиПоискаФайлов()
    настройкиПоискаФайлов.ИсключитьФайлы(Истина)
    настройкиПоискаФайлов.МаксимальнаяГлубина(1)

    для каталогПлатформы из каталогиПлатформы
        попытка
            знч найденныеФайлы = Файлы.Найти(каталогПлатформы, настройкиПоискаФайлов)
            для найденныйФайл из найденныеФайлы
                если не найденныйФайл.Имя.НачинаетсяС("8.3")
                    продолжить
                ;
                установленныеПлатформы.Вставить(найденныйФайл.Имя, найденныйФайл.Путь)
            ;
        поймать Исключение: ИсключениеФайловойСистемы 
            // не обрабатываем отсутствие каталога 1Cv8
        ;
    ;

    возврат установленныеПлатформы
;

@Локально
метод МаксимальныеПлатформы(): Соответствие<Строка, Строка>
        знч установленныеПлатформы = УстановленныеПлатформы()

    пер максимальныеПлатформы: Соответствие<Строка, Строка>

    для платформа из установленныеПлатформы
        знч разложенныйТекущийРелиз = платформа.Ключ.Разделить(".")

        пер релизБезВерсииМассивом = новый Массив<Строка>(разложенныйТекущийРелиз)
        релизБезВерсииМассивом.УдалитьПоИндексу(3)

        знч релизБезВерсии = Строки.Соединить(релизБезВерсииМассивом, ".")

        если не максимальныеПлатформы.СодержитКлюч(релизБезВерсии)
            максимальныеПлатформы.Вставить(релизБезВерсии, платформа.Значение)
        продолжить
        ;

        знч последнийРелиз = максимальныеПлатформы.Получить(релизБезВерсии)
        знч разложенныйПоследнийРелиз = последнийРелиз.Разделить(".")

        если разложенныйТекущийРелиз[3] > разложенныйПоследнийРелиз[3]
            максимальныеПлатформы.Вставить(релизБезВерсии, платформа.Значение)
        ;
    ;

    возврат максимальныеПлатформы
;

@Локально
метод КаталогиПлатформы(): Массив<Строка>
    
    пер Каталоги: Массив<Строка>
    знч используемаяОС = ИспользуемаяОС()
    знч СР = Файлы.СимволРазделителя

    выбор используемаяОС
    когда ОперационныеСистемы.Windows
        знч Каталог32 = СредаИсполнения.ПолучитьПеременную("ProgramFiles(x86)")
        Каталоги.Добавить("%{Каталог32}%{СР}1Cv8")
        знч Каталог64 = СредаИсполнения.ПолучитьПеременную("ProgramFiles")
        Каталоги.Добавить("%{Каталог64}%{СР}1Cv8")
    когда ОперационныеСистемы.Linux
        Каталоги.Добавить("%{СР}opt%{СР}1cv8%{СР}x86_64")
    ;

    возврат Каталоги
;