#!/usr/bin/executor

#требуется IConfigurationProperties.sbsl

@Глобально
метод ПеревестиВНовыйФорматParentConfigurations(ПутьКФайламПроекта: Строка)

    знч СР = Файлы.СимволРазделителя
    знч ФайлИзменений   = ПолучитьФайлParentConfigurations(ПутьКФайламПроекта)
    если не ФайлИзменений.Существует()
        возврат
    ;
    пер ДанныеИзменения = ФайлИзменений.ОткрытьПотокЧтения().ПрочитатьКакСтроку()

    если ДанныеИзменения.Содержит(Символы.НОВАЯ_СТРОКА)
        возврат
    ;
    ДанныеИзменения = ДанныеИзменения.Заменить(",", "," + Символы.НОВАЯ_СТРОКА)

    ФайлИзменений.ОткрытьПотокЗаписи(Ложь).Записать(Байты{EF BB BF}).Записать(ДанныеИзменения).Закрыть()
    Консоль.Записать("\нИзменен файл %{ПутьКФайламПроекта}%{СР}Ext%{СР}ParentConfigurations.bin")

    возврат
;

@Глобально
метод ПеревестиВНовыйФорматConfigDumpInfo(ПутьКФайламПроекта: Строка)

    знч СР              = Файлы.СимволРазделителя
    знч ФайлИзменений   = ПолучитьФайлConfigDumpInfo(ПутьКФайламПроекта)
    если не ФайлИзменений.Существует()
        возврат
    ;
    пер ДанныеИзменения = ФайлИзменений.ОткрытьПотокЧтения().ПрочитатьКакСтроку()
    знч ОбразецЗамены   = новый Образец("configVersion=\".*\"")

    ДанныеИзменения = ДанныеИзменения.Заменить(ОбразецЗамены, "configVersion=\"0000000000000000000000000000000000000000\"")

    ФайлИзменений.ОткрытьПотокЗаписи(Ложь).Записать(Байты{EF BB BF}).Записать(ДанныеИзменения).Закрыть()
    Консоль.Записать("\нИзменен файл %{ПутьКФайламПроекта}%{СР}ConfigDumpInfo.xml")

    возврат
;

@Глобально
метод ПолучитьФайлыОбъектаМетаданных(ПутьКФайламПроекта: Строка, ИмяОбъектаМетаданных: Строка, ПолучитьФайлыРодителя = Ложь) : Соответствие<Строка, Строка>

    знч СР = Файлы.СимволРазделителя
    знч ФайлыОбъекта = новый Соответствие<Строка, Строка>()
    знч СоставИмениМетаданных = ИмяОбъектаМетаданных.Разделить(".")
    пер ОсновнойФайлПуть = ""
    пер ОсновнойКаталогПуть = ""

    если СоставИмениМетаданных[0] == "Configuration"
        ОсновнойКаталогПуть = "%{ПутьКФайламПроекта}%{СР}Ext"
        ОсновнойФайлПуть = "%{ПутьКФайламПроекта}%{СР}Configuration.xml"
    иначе если СоставИмениМетаданных[0] == "Subsystem"
        ОсновнойКаталогПуть = "%{ПутьКФайламПроекта}"
        для Индекс = 0 по СоставИмениМетаданных.Размер() - 1
            если Индекс % 2 == 0
                ОсновнойКаталогПуть = ОсновнойКаталогПуть + "%{СР}%{ПутьКОбъектуМетаданных(СоставИмениМетаданных[Индекс])}"
            иначе
                ОсновнойКаталогПуть = ОсновнойКаталогПуть + "%{СР}%{СоставИмениМетаданных[Индекс]}"
            ;
        ;
        ОсновнойФайлПуть = "%{ОсновнойКаталогПуть}.xml"
    иначе
        если СоставИмениМетаданных.Размер() == 2 или ПолучитьФайлыРодителя
            ОсновнойКаталогПуть = "%{ПутьКФайламПроекта}%{СР}%{ПутьКОбъектуМетаданных(СоставИмениМетаданных[0])}%{СР}%{СоставИмениМетаданных[1]}"
        иначе если СоставИмениМетаданных.Размер() == 4
            ОсновнойКаталогПуть = "%{ПутьКФайламПроекта}%{СР}%{ПутьКОбъектуМетаданных(СоставИмениМетаданных[0])}%{СР}%{СоставИмениМетаданных[1]}%{СР}%{ПутьКОбъектуМетаданных(СоставИмениМетаданных[2])}%{СР}%{СоставИмениМетаданных[3]}"
        иначе
            Консоль.Записать("Не определен путь к объекту метаданных: %{ИмяОбъектаМетаданных}")
            выбросить новый ИсключениеНедопустимоеСостояние()
        ;
        ОсновнойФайлПуть = "%{ОсновнойКаталогПуть}.xml"
    ;

    ФайлыОбъекта.Вставить("ПутьКОсновномуФайлу",    ОсновнойФайлПуть)
    ФайлыОбъекта.Вставить("ПутьКОсновномуКаталогу", ОсновнойКаталогПуть)

    возврат ФайлыОбъекта
;

@Глобально
метод ПрочитатьНастройкиПоддержки(ПутьКФайламКонфигурации: Строка) : IConfigurationProperties.НастройкиПоддержки

    знч НастройкиПоддержки = новый IConfigurationProperties.НастройкиПоддержки()

    знч ФайлИзменений   = ПолучитьФайлParentConfigurations(ПутьКФайламКонфигурации)
    если не ФайлИзменений.Существует()
        возврат НастройкиПоддержки
    ;

    знч ДанныеИзменения = ФайлИзменений.ОткрытьПотокЧтения().ПрочитатьКакСтроку().Разделить("," + Символы.НОВАЯ_СТРОКА, Истина)
    знч КоличествоДанныхИзменения = ДанныеИзменения.Размер()
    знч Граница = КоличествоДанныхИзменения - 15
    пер Индекс  = 0

    пока Истина

        если Индекс <= 9
            НастройкиПоддержки.НачальныеДанные.Добавить(ДанныеИзменения[Индекс])
        иначе если Индекс >= Граница
            НастройкиПоддержки.КонечныеДанные.Добавить(ДанныеИзменения[Индекс])
        иначе если (Индекс - 10)%4 > 0
            // ни чего не выполняем
        иначе

            знч Идентификатор = новый IConfigurationProperties.Идентификатор(
                ДанныеИзменения[Индекс + 2],
                ДанныеИзменения[Индекс + 3],
                "",
                ДанныеИзменения[Индекс],
                ДанныеИзменения[Индекс + 1])

            если не Идентификатор.ГУИД == Идентификатор.ГУИДПоставщика
                Идентификатор.Исправить = Истина
            ;

            НастройкиПоддержки.Идентификаторы.Вставить(
                ДанныеИзменения[Индекс + 2],
                Идентификатор
            )
        ;

        Индекс += 1
        если Индекс == КоличествоДанныхИзменения
            прервать
        ;
    ;

    возврат НастройкиПоддержки
;

@Глобально
метод ИсправитьНастройкиПоддержки(СтуркутураКонфигурации: IConfigurationProperties.СтуркутураКонфигурации, ОбновитьФайл = Истина)

    если СтуркутураКонфигурации.НастройкиПоддержки.НачальныеДанные.Размер() == 0
        возврат
    ;

    знч НастройкиПоддержкиТекст = новый Массив<Строка>()

    для НачальныеДанные из СтуркутураКонфигурации.НастройкиПоддержки.НачальныеДанные
        НастройкиПоддержкиТекст.Добавить(НачальныеДанные)
    ;

    пер КоличествоИдентификаторов = 0
    знч ИндексСтрокиРазмера = 9
    пер ВывестиОшибкаИдентификаторов = Истина

    для КлючИЗначение из СтуркутураКонфигурации.НастройкиПоддержки.Идентификаторы

        знч Идентификатор = КлючИЗначение.Значение

        если Идентификатор.Имя.Пусто()
            если ВывестиОшибкаИдентификаторов
                Консоль.Записать("\н*****************************************************************")
                Консоль.Записать("Ошибка идентификаторов bin")
                ВывестиОшибкаИдентификаторов = Ложь
            ;
            Консоль.Записать(Идентификатор.ГУИД)
            продолжить
        ;

        если не ВывестиОшибкаИдентификаторов
            Консоль.Записать("*****************************************************************")
            ВывестиОшибкаИдентификаторов = Истина
        ;

        если Идентификатор.Исправить
            Консоль.Записать("\н*****************************************************************")
            Консоль.Записать("Расхождение идентификатора")
            Консоль.Записать(Идентификатор.Имя)
            Консоль.Записать("%{Идентификатор.ГУИД} , %{Идентификатор.ГУИДПоставщика}")
            Консоль.Записать("*****************************************************************")
        ;

        если не Идентификатор.ПравилаПользователя == "0"
            Консоль.Записать(Идентификатор.Имя)
            Консоль.Записать(Идентификатор.ПравилаПользователя + ","
                            + Идентификатор.ПравилаПоставщика + ","
                            + Идентификатор.ГУИД + ","
                            + Идентификатор.ГУИДПоставщика)
        ;

        НастройкиПоддержкиТекст.Добавить(Идентификатор.ПравилаПользователя)
        НастройкиПоддержкиТекст.Добавить(Идентификатор.ПравилаПоставщика)
        НастройкиПоддержкиТекст.Добавить(Идентификатор.ГУИД)
        НастройкиПоддержкиТекст.Добавить(Идентификатор.ГУИДПоставщика)

        КоличествоИдентификаторов = КоличествоИдентификаторов + 1

    ;

    НастройкиПоддержкиТекст[ИндексСтрокиРазмера] = КоличествоИдентификаторов.ВСтроку()

    пер ВывестиНеНаходитсяНаПоддержке = Истина

    для КлючИЗначение из СтуркутураКонфигурации.Метаданные
        знч ОбъектМетаданных = КлючИЗначение.Значение
        если не ОбъектМетаданных.Идентификатор.ПравилаПользователя.Пусто()
            продолжить
        ;
        если ВывестиНеНаходитсяНаПоддержке
            Консоль.Записать("\н*****************************************************************")
            Консоль.Записать("Не находится на поддержке")
            ВывестиНеНаходитсяНаПоддержке = Ложь
        ;
        Консоль.Записать("%{ОбъектМетаданных.Имя} : %{ОбъектМетаданных.Идентификатор.ГУИД}")
    ;

    если не ВывестиНеНаходитсяНаПоддержке
        Консоль.Записать("*****************************************************************")
    ;

    для КонечныеДанные из СтуркутураКонфигурации.НастройкиПоддержки.КонечныеДанные
        НастройкиПоддержкиТекст.Добавить(КонечныеДанные)
    ;

    если ОбновитьФайл
        знч ФайлИзменений   = ПолучитьФайлParentConfigurations(СтуркутураКонфигурации.ПутьКФайламКонфигурации)
        ФайлИзменений.ОткрытьПотокЗаписи(Ложь).Записать(Байты{EF BB BF}).Записать(Строки.Соединить(НастройкиПоддержкиТекст, "," + Символы.НОВАЯ_СТРОКА)).Закрыть()
        Консоль.Записать("\нИзменен файл идентификаторов bin")
    ;

;

@Глобально
метод ПрочитатьСтруктуруКонфигурации(ПутьКФайламКонфигурации: Строка, РежимОбработки: IConfigurationProperties.РежимОбработки) : IConfigurationProperties.СтуркутураКонфигурации

    знч ВремяНачала = ДатаВремя.Сейчас()

    знч ConfigDumpInfo = ПолучитьФайлConfigDumpInfo(ПутьКФайламКонфигурации)
    знч СтуркутураКонфигурации = новый IConfigurationProperties.СтуркутураКонфигурации(ПутьКФайламКонфигурации)

    если не ConfigDumpInfo.Существует()
        возврат СтуркутураКонфигурации
    ;

    знч ПолучитьПоПоддержке = (РежимОбработки == IConfigurationProperties.РежимОбработки.ПолучитьПоПоддержке)

    СтуркутураКонфигурации.НастройкиПоддержки = ПрочитатьНастройкиПоддержки(ПутьКФайламКонфигурации)

    пер Уровень = 0
    пер ДанныеОбъектаРодителя: IConfigurationProperties.ОбъектМетаданных

    исп ПотокЧтенияConfigDumpInfo = ConfigDumpInfo.ОткрытьПотокЧтения()
    знч ЧтениеXml = новый ЧтениеXml(ПотокЧтенияConfigDumpInfo)
    пока ЧтениеXml.Следующий()

        если (ЧтениеXml.Имя == "Metadata"
            и ЧтениеXml.ВидУзла == ВидУзлаXml.КонецЭлемента)
            Уровень -= 1
        ;
        если не (ЧтениеXml.Имя == "Metadata"
            и ЧтениеXml.ВидУзла == ВидУзлаXml.НачалоЭлемента)
            продолжить
        ;

        Уровень += 1

        знч ИмяМетаданных = ЧтениеXml.ЗначениеАтрибута("name")
        пер ГУИД          = ЧтениеXml.ЗначениеАтрибута("id")
        знч Версия        = ЧтениеXml.ЗначениеАтрибута("configVersion")
        знч СоставГУИД    = ГУИД.Разделить(".")
        знч СоставИмени   = ИмяМетаданных.Разделить(".")
        знч ЭтоРодитель   = (Уровень == 1) и (СоставГУИД.Размер() == 1) и (СоставИмени.Размер() == 2)
        пер Файлы         = новый Соответствие<Строка, Строка>()
        если (Уровень == 1) и (СоставГУИД.Размер() == 1)
            Файлы         = ПолучитьФайлыОбъектаМетаданных(ПутьКФайламКонфигурации, ИмяМетаданных)
        ;

        знч Идентификатор = СтуркутураКонфигурации.НастройкиПоддержки.Идентификаторы.ПолучитьИлиУмолчание(
            СоставГУИД[0],
            новый IConfigurationProperties.Идентификатор(СоставГУИД[0],
            "",
            "",
            "1",
            "0"))

        Идентификатор.Имя = ИмяМетаданных

        знч ДанныеОбъекта = новый IConfigurationProperties.ОбъектМетаданных(
            Идентификатор,
            ИмяМетаданных,
            Версия == Неопределено ? "" : Версия,
            Ложь,
            Ложь,
            новый Массив<IConfigurationProperties.ОбъектМетаданных>(),
            Файлы
        )

        если ЭтоРодитель
            ДанныеОбъектаРодителя = ДанныеОбъекта
        иначе
            ДанныеОбъектаРодителя.Элементы.Добавить(ДанныеОбъекта)
            ДанныеОбъектаРодителя.ОтличаютсяПравилаПользователяПодчиненных =
                ДанныеОбъектаРодителя.ОтличаютсяПравилаПользователяПодчиненных
                или ДанныеОбъектаРодителя.Идентификатор.ПравилаПользователя !=
                    ДанныеОбъекта.Идентификатор.ПравилаПользователя
            ДанныеОбъектаРодителя.ОтличаютсяПравилаПоставщикаПодчиненных =
                ДанныеОбъектаРодителя.ОтличаютсяПравилаПоставщикаПодчиненных
                или ДанныеОбъектаРодителя.Идентификатор.ПравилаПоставщика !=
                    ДанныеОбъекта.Идентификатор.ПравилаПоставщика
        ;

        если ПолучитьПоПоддержке и Идентификатор.ПравилаПользователя == "0" и не ДанныеОбъектаРодителя.ОтличаютсяПравилаПользователяПодчиненных
            продолжить
        ;

        СтуркутураКонфигурации.Метаданные.ВставитьЕслиОтсутствует(ДанныеОбъектаРодителя.Идентификатор.ГУИД, ДанныеОбъектаРодителя)

    ;

    ПотокЧтенияConfigDumpInfo.Закрыть()

    знч ВремяОкончания = ДатаВремя.Сейчас()

    Консоль.Записать("Время анализа структуры конфигурации (%{(ВремяОкончания - ВремяНачала).Представление("с")} сек.)")

    возврат СтуркутураКонфигурации
;

@Глобально
метод ОбработатьФайлыКонфигурации(СтуркутураКонфигурации: IConfigurationProperties.СтуркутураКонфигурации, ПутьКФайламКонфигурации: Строка, ПутьКФайламРезультата: Строка, РежимОбработки: IConfigurationProperties.РежимОбработки)

    знч ВремяНачала = ДатаВремя.Сейчас()

    знч СР = Файлы.СимволРазделителя
    пер ТекстФайлыДляЗагрузки = ""
    пер ТекстФайлыДляВыгрузки = ""

    знч ПолучитьПоПоддержке = РежимОбработки == IConfigurationProperties.РежимОбработки.ПолучитьПоПоддержке
    знч ПолучитьФайлыКонфигурации = ПолучитьПоПоддержке и не ПутьКФайламРезультата.Пусто()

    знч ХешированиеДанныхКонфигурации = новый ХешированиеДанных(АлгоритмХеширования.Md5)

    ПеревестиВНовыйФорматConfigDumpInfo(ПутьКФайламКонфигурации)
    ПеревестиВНовыйФорматParentConfigurations(ПутьКФайламКонфигурации)

    для КлючИЗначение из СтуркутураКонфигурации.Метаданные

        знч ОбъектМетаданных = КлючИЗначение.Значение

        если ПолучитьПоПоддержке
            и ОбъектМетаданных.Идентификатор.ПравилаПользователя == "0"
            и не ОбъектМетаданных.ОтличаютсяПравилаПользователяПодчиненных
            продолжить
        ;

        знч ОсновнойФайл = новый Файл(ОбъектМетаданных.Файлы.Получить("ПутьКОсновномуФайлу"))
        если не ОсновнойФайл.Существует()
            Консоль.Записать(ОсновнойФайл.Путь)
            Консоль.Записать("Не существует путь к объекту метаданных: %{ОбъектМетаданных.Имя}")
            выбросить новый ИсключениеНедопустимоеСостояние()
        ;

        если ПолучитьФайлыКонфигурации
            знч ОсновнойФайлОтносительныйПуть = ОсновнойФайл.Путь.Заменить(ПутьКФайламКонфигурации, "")
            знч ОсновнойФайлРезультат = новый Файл(ПутьКФайламРезультата + СР + ОсновнойФайлОтносительныйПуть)
            если ОсновнойФайлРезультат.Существует()
                Файлы.Удалить(ОсновнойФайлРезультат)
            ;
            Файлы.Скопировать(ОсновнойФайл, ОсновнойФайлРезультат)
            если ОсновнойФайл.Имя == "Configuration.xml"
                знч ФайлConfigDumpInfo     = новый Файл(ОсновнойФайл.Путь.Заменить("Configuration.xml", "ConfigDumpInfo.xml"))
                знч ФайлConfigDumpInfoТемп = новый Файл(ОсновнойФайлРезультат.Путь.Заменить("Configuration.xml", "ConfigDumpInfo.xml"))
                Файлы.Скопировать(ФайлConfigDumpInfo, ФайлConfigDumpInfoТемп)
            ;
            ТекстФайлыДляЗагрузки = ТекстФайлыДляЗагрузки + ОсновнойФайлОтносительныйПуть + Символы.НОВАЯ_СТРОКА
            если ОбъектМетаданных.Имя.НачинаетсяС("Configuration")
                ТекстФайлыДляЗагрузки = ТекстФайлыДляЗагрузки + "Ext%{СР}ParentConfigurations.bin" + Символы.НОВАЯ_СТРОКА
                ТекстФайлыДляВыгрузки = ТекстФайлыДляВыгрузки + ОбъектМетаданных.Имя + ".ParentConfigurations" + Символы.НОВАЯ_СТРОКА
            ;
        ;

        IConfigurationProperties.РассчитатьХешФайла(ОсновнойФайл.Путь, ХешированиеДанныхКонфигурации)
        если ОсновнойФайл.Имя == "Configuration.xml"
            знч ПутьConfigDumpInfo = ОсновнойФайл.Путь.Заменить("Configuration.xml", "ConfigDumpInfo.xml")
            IConfigurationProperties.РассчитатьХешФайла(ПутьConfigDumpInfo, ХешированиеДанныхКонфигурации)
        ;

        знч ОсновнойКаталог     = новый Файл(ОбъектМетаданных.Файлы.Получить("ПутьКОсновномуКаталогу"))
        если ОсновнойКаталог.Существует() и не ОсновнойКаталог.ЭтоКаталог()
            Консоль.Записать(ОсновнойКаталог.Путь)
            Консоль.Записать("Ошибочный путь к каталогу объекта метаданных: %{ОбъектМетаданных.Имя}")
            выбросить новый ИсключениеНедопустимоеСостояние()
        иначе если ОсновнойКаталог.Существует() и ОсновнойКаталог.ЭтоКаталог()
            если ПолучитьФайлыКонфигурации
                знч ОсновнойКаталогРезультат = новый Файл(ПутьКФайламРезультата + СР + ОсновнойКаталог.Путь.Заменить(ПутьКФайламКонфигурации, ""))
                если ОсновнойКаталогРезультат.Существует()
                    Файлы.Удалить(ОсновнойКаталогРезультат)
                ;
                Файлы.Скопировать(ОсновнойКаталог.Путь, ОсновнойКаталогРезультат)
                если ОсновнойФайл.Имя == "Configuration.xml"
                    знч ФайлКонфигурации = ПолучитьФайлПоставщика(ПутьКФайламРезультата)
                    Файлы.Удалить(ФайлКонфигурации)
                ;
            ;
            IConfigurationProperties.РассчитатьХешКаталога(ОсновнойКаталог.Путь, ХешированиеДанныхКонфигурации, Ложь)
        ;
    ;

    СтуркутураКонфигурации.Хеш                   = ХешированиеДанныхКонфигурации.Результат.ВСтроку()
    СтуркутураКонфигурации.ТекстФайлыДляЗагрузки = ТекстФайлыДляЗагрузки
    СтуркутураКонфигурации.ТекстФайлыДляВыгрузки = ТекстФайлыДляВыгрузки

    знч ВремяОкончания = ДатаВремя.Сейчас()
    Консоль.Записать("Время обработки структуры конфигурации (%{(ВремяОкончания - ВремяНачала).Представление("с")} сек.)")

;


@Глобально
метод ПолучитьСписокИзмененныхОбъектов(ПутьКФайламКонфигурации: Строка, ПутьКФайламИзмененнойКонфигурации: Строка, РежимОбработки: IConfigurationProperties.РежимОбработки)

    знч СтуркутураКонфигурации = ПрочитатьСтруктуруКонфигурации(
        ПутьКФайламКонфигурации,
        РежимОбработки)

    знч ИзмСтуркутураКонфигурации = ПрочитатьСтруктуруКонфигурации(
        ПутьКФайламИзмененнойКонфигурации,
        РежимОбработки)

    для КлючИЗнчаение из ИзмСтуркутураКонфигурации.Метаданные

        знч ОбъектМетаданных = СтуркутураКонфигурации.Метаданные.Получить(КлючИЗнчаение.Ключ)
        если ОбъектМетаданных.Хеш == КлючИЗнчаение.Значение.Хеш
            СтуркутураКонфигурации.Метаданные.Удалить(КлючИЗнчаение.Ключ)
        ;

    ;

;

@Глобально
метод ПолучитьФайлПоставщика(ПутьКФайламКонфигурации: Строка): Файл

    знч СР = Файлы.СимволРазделителя
    знч НастройкиПоискаФайлов = новый НастройкиПоискаФайлов()
            .ИмяСодержит(".cf")
            .МаксимальнаяГлубина(1)
            .ИсключитьФайлы(Ложь)
            .ИсключитьКаталоги(Истина)

    знч КаталогParentConfigurations = новый Файл(ПутьКФайламКонфигурации + "%{СР}Ext%{СР}ParentConfigurations")
    если не КаталогParentConfigurations.Существует()
        возврат КаталогParentConfigurations
    ;

    знч ФайлПоставщика = Файлы.Найти(КаталогParentConfigurations, НастройкиПоискаФайлов)[0]

    возврат ФайлПоставщика

;

@Глобально
метод ПолучитьФайлConfigDumpInfo(ПутьКФайламКонфигурации: Строка): Файл

    знч СР = Файлы.СимволРазделителя
    знч ФайлConfigDumpInfo = новый Файл(ПутьКФайламКонфигурации + "%{СР}ConfigDumpInfo.xml")

    возврат ФайлConfigDumpInfo

;

@Глобально
метод ПолучитьФайлParentConfigurations(ПутьКФайламКонфигурации: Строка): Файл

    знч СР = Файлы.СимволРазделителя
    знч ФайлParentConfigurations = новый Файл(ПутьКФайламКонфигурации + "%{СР}Ext%{СР}ParentConfigurations.bin")

    возврат ФайлParentConfigurations

;

@Локально
метод ПутьКОбъектуМетаданных(ТипОбъектаМетаданных: Строка) : Строка

    выбор ТипОбъектаМетаданных
    когда "BusinessProcess"
        возврат "BusinessProcesses"
    когда "ChartOfAccounts"
        возврат "ChartsOfAccounts"
    когда "ChartOfCalculationTypes"
        возврат "ChartsOfCalculationTypes"
    когда "ChartOfCharacteristicTypes"
        возврат "ChartsOfCharacteristicTypes"
    когда "FilterCriterion"
        возврат "FilterCriteria"
    когда "Template"
        возврат "Templates"
    иначе
        возврат "%{ТипОбъектаМетаданных}s"
    ;
;