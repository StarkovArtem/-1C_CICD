#!/usr/bin/executor

#требуется IProjectProperties.sbsl
#требуется IIBOperation.sbsl

@Глобально
метод СобратьФайлыКонфигурации(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта, СобратьРасширения: Булево = Ложь, ПересобратьКонфигурациюПоставщика: Булево = Ложь) : Булево

    знч СобранныеПроекты = новый Массив<Строка>()
    пер Статус = Истина

    если ПараметрыПроекта.ПутьКФайламКонфигурации.Путь.Пусто()
        возврат Статус
    ;

    если не СобратьРасширения
        знч ВремяНачала = ДатаВремя.Сейчас()
        знч СтатусПолучения = ПолучитьФайлКонфигурации(
            ПараметрыПроекта,
            ПараметрыПроекта.ПутьКФайламКонфигурации,
            ПересобратьКонфигурациюПоставщика)
        знч ВремяОкончания = ДатаВремя.Сейчас()
        если СтатусПолучения == 1
            СобранныеПроекты.Добавить("%{IProjectProperties.ИМЯ_КАТАЛОГА_КОНФИГУРАЦИИ} (%{(ВремяОкончания - ВремяНачала).Представление("с")} сек.)")
        ;
        Статус = Статус и СтатусПолучения < 2
    ;

    для Расширение из ПараметрыПроекта.Расширения
        если не СобратьРасширения
            прервать
        ;
        знч ВремяНачала = ДатаВремя.Сейчас()
        знч СтатусПолучения = ПолучитьФайлКонфигурации(
            ПараметрыПроекта,
            Расширение.Значение,
            Ложь)
        знч ВремяОкончания = ДатаВремя.Сейчас()
        если СтатусПолучения == 1
            СобранныеПроекты.Добавить(Расширение.Ключ + " (%{(ВремяОкончания - ВремяНачала).Представление("с")} сек.)")
        ;
        Статус = Статус и СтатусПолучения < 2
    ;

    ВывестиИнформациюПоСобраннымПроектам(СобранныеПроекты)

    возврат Статус
;

@Глобально
метод СобратьПатчКонфигурации(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта): Число

    знч СобранныеПроекты = новый Массив<Строка>()

    если ПараметрыПроекта.ПутьКФайламКонфигурации.Путь.Пусто()
        возврат 1
    ;

    знч СР                  = Файлы.СимволРазделителя
    знч КаталогСборкиПатча  = новый Файл("%{ПараметрыПроекта.ПутьКВременнымФайлам.Путь}%{СР}%{IProjectProperties.ИМЯ_КАТАЛОГА_ПАТЧА}")
    знч ФайлСборкиПатча     = новый Файл("%{ПараметрыПроекта.ПутьКСборкеПроекта.Путь}%{СР}%{IProjectProperties.ИМЯ_КАТАЛОГА_ПАТЧА}.zip")
    знч ФайлЗагрузки        = новый Файл("%{ФайлСборкиПатча.Путь.Заменить(".zip", "_in")}")
    знч ФайлВыгрузки        = новый Файл("%{ФайлСборкиПатча.Путь.Заменить(".zip", "_out")}")

    если КаталогСборкиПатча.Существует()
        Файлы.Удалить(КаталогСборкиПатча, Истина)
    ;

    пер СтуркутураКонфигурации: IConfigurationProperties.СтуркутураКонфигурации|Неопределено
    попытка
        СтуркутураКонфигурации = ПрочитатьСтруктуруКонфигурации(ПараметрыПроекта.ПутьКФайламКонфигурации.Путь, КаталогСборкиПатча.Путь)
    поймать Исключение: Исключение
        СтуркутураКонфигурации = Неопределено
        если не Исключение.Описание.Пусто()
            Консоль.Записать("\н#################################################################")
            Консоль.Записать(Исключение.Описание)
            Консоль.Записать("#################################################################")
        ;
    ;

    если СтуркутураКонфигурации == Неопределено
        возврат 2
    ;

    если СтуркутураКонфигурации.Хеш == ПараметрыПроекта.ПутьКФайламКонфигурации.Хеш
        и ФайлСборкиПатча.Существует()
        и ФайлЗагрузки.Существует()
        и ФайлВыгрузки.Существует()
        возврат 1
    иначе
        ПараметрыПроекта.ПутьКФайламКонфигурации.Хеш = СтуркутураКонфигурации.Хеш
    ;

    знч Архив = новый ФайлZip(Архив = ФайлСборкиПатча.Путь, КодировкаИменФайлов = Кодировка.Utf8)
    для Файл из КаталогСборкиПатча.Дочерние
        Архив.Добавить(Файл)
    ;

    ФайлЗагрузки.ОткрытьПотокЗаписи().Записать(СтуркутураКонфигурации.ТекстФайлыДляЗагрузки).Закрыть()

    ФайлВыгрузки.ОткрытьПотокЗаписи().Записать(СтуркутураКонфигурации.ТекстФайлыДляВыгрузки).Закрыть()

    СобранныеПроекты.Добавить(IProjectProperties.ИМЯ_КАТАЛОГА_ПАТЧА)

    ВывестиИнформациюПоСобраннымПроектам(СобранныеПроекты)

    возврат 0
;

@Глобально
метод ОбновитьФайлыКонфигурации(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта, ТипЗагрузки = 1, ПолучитьКонфигурациюПоставщика = Ложь, ПолучитьФайлСравнения = Ложь) : Булево

    если ПараметрыПроекта.ПараметрыСоединенияИБ.СтрокаСоединения.Пусто()
        Консоль.Записать("Не заполнен путь к ИБ в файле хэша ProjectsHash.json")
        возврат Ложь
    ;

    если ТипЗагрузки == 1
        ЗагрузитьКонфигурацию(ПараметрыПроекта, ПараметрыПроекта.ПараметрыСоединенияИБ, Ложь)
        ПолучитьФайлыКонфигурации(ПараметрыПроекта, Ложь, 2)
    иначе если ТипЗагрузки == 2
        ЗагрузитьКонфигурацию(ПараметрыПроекта, ПараметрыПроекта.ПараметрыСоединенияИБ, Истина)
    иначе
        ЗагрузитьПатчКонфигурации(ПараметрыПроекта, ПараметрыПроекта.ПараметрыСоединенияИБ, ПолучитьКонфигурациюПоставщика, ПолучитьФайлСравнения)
    ;

    возврат Истина
;

метод ПолучитьФайлКонфигурации(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта, ПутьКФайламПроекта: IProjectProperties.ПутьКФайламПроекта, ПересобратьКонфигурациюПоставщика: Булево = Ложь): Число

    пер Результат = 0
    знч СР = Файлы.СимволРазделителя
    знч ПутьКФайламПлатформы         = ПараметрыПроекта.ПутьКФайламПлатформы
    знч ИспользуетсяАвтономныйСервер = ПараметрыПроекта.ИспользуетсяАвтономныйСервер
    знч РасширениеФайлаСборки        = ПутьКФайламПроекта.ЭтоРасширение ? "cfe" : "cf"
    знч ФайлСборки                   = новый Файл(ПараметрыПроекта.ПутьКСборкеПроекта.Путь + СР + (ПутьКФайламПроекта.ЭтоРасширение ? "%{ПутьКФайламПроекта.Имя}.%{РасширениеФайлаСборки}" : "1Cv8.%{РасширениеФайлаСборки}"))
    знч КаталогВременнойБазы         = ПолучитьВременныйКаталог(ПараметрыПроекта, IProjectProperties.ИМЯ_КАТАЛОГА_КОНФИГУРАЦИИ)
    знч СоздатьИнформационнуюБазу    = (не ПутьКФайламПроекта.ЭтоРасширение
        или ПараметрыПроекта.РежимСборкиПроекта == IProjectProperties.РежимыСборкиПроекта.ВОсновнойКонфигурации
        и КаталогВременнойБазы.Дочерние.Размер() == 0
        или ПараметрыПроекта.РежимСборкиПроекта == IProjectProperties.РежимыСборкиПроекта.ВПустойКонфигурации)

    пер СтруктураКонфигурации = ПрочитатьСтруктуруКонфигурации(ПутьКФайламПроекта.Путь, ИсправитьОшибки = ПересобратьКонфигурациюПоставщика)
    если не ПересобратьКонфигурациюПоставщика
        и СтруктураКонфигурации.Хеш == ПутьКФайламПроекта.Хеш
        и ФайлСборки.Существует()
        и (ПараметрыПроекта.РежимСборкиПроекта == IProjectProperties.РежимыСборкиПроекта.ВОсновнойКонфигурации
            и КаталогВременнойБазы.Дочерние.Размер() > 0
            или ПараметрыПроекта.РежимСборкиПроекта == IProjectProperties.РежимыСборкиПроекта.ВПустойКонфигурации)
        Консоль.Записать("%{ПутьКФайламПроекта.Имя}: изменений нет")
        возврат Результат
    ;

    попытка

        если ПересобратьКонфигурациюПоставщика

            знч ВременныйКаталогБазыПоставщика = ПолучитьВременныйКаталог(ПараметрыПроекта, IProjectProperties.ИМЯ_КАТАЛОГА_КОНФИГУРАЦИИ_ПОСТАВЩИКА)
            если ВременныйКаталогБазыПоставщика.Существует()
                Файлы.Удалить(ВременныйКаталогБазыПоставщика)
            ;
            знч ВременныйКаталогВыгрузки       = ПолучитьВременныйКаталог(ПараметрыПроекта, "%{IProjectProperties.ИМЯ_КАТАЛОГА_КОНФИГУРАЦИИ}_xml")
            если ВременныйКаталогВыгрузки.Существует()
                Файлы.Удалить(ВременныйКаталогВыгрузки)
            ;
            знч ФайлКонфигурацииПоставщика  = IConfiguration.ПолучитьФайлПоставщика(ПутьКФайламПроекта.Путь)
            
            Консоль.Записать("\нCreateInfobase:")
            IIBOperation.СоздатьИнформационнуюБазу(
                ВременныйКаталогБазыПоставщика.Путь,
                ПутьКФайламПлатформы,
                ИспользуетсяАвтономныйСервер)

            Консоль.Записать("\нLoadFromFile:")
            IIBOperation.ЗагрузитьКонфигурацию(
                ФайлКонфигурацииПоставщика.Путь,
                ВременныйКаталогБазыПоставщика.Путь,
                ПутьКФайламПлатформы,
                ИспользуетсяАвтономныйСервер,
                "",
                "",
                ПутьКФайламПроекта.Имя)

            Консоль.Записать("\нDumpFiles:")
            IIBOperation.ВыгрузитьФайлыКонфигурации(
                ВременныйКаталогВыгрузки.Путь,
                ВременныйКаталогБазыПоставщика.Путь,
                ПутьКФайламПлатформы,
                ИспользуетсяАвтономныйСервер,
                "",
                "",
                ПутьКФайламПроекта.Имя)

            Файлы.Удалить(ВременныйКаталогБазыПоставщика)
            Консоль.Записать("ИБ поставщика \"%{ВременныйКаталогБазыПоставщика.Путь}\" удалена\н")

            Консоль.Записать("\нCreateInfobase:")
            IIBOperation.СоздатьИнформационнуюБазу(
                ВременныйКаталогБазыПоставщика.Путь,
                ПутьКФайламПлатформы,
                ИспользуетсяАвтономныйСервер)

            Консоль.Записать("\нLoadFromFiles:")
            IIBOperation.ЗагрузитьФайлыКонфигурации(
                ВременныйКаталогВыгрузки.Путь,
                ВременныйКаталогБазыПоставщика.Путь,
                ПутьКФайламПлатформы,
                ИспользуетсяАвтономныйСервер,
                "",
                "",
                ПутьКФайламПроекта.Имя)

            IConfiguration.ОбработатьФайлыКонфигурации(
                СтруктураКонфигурации,
                ПутьКФайламПроекта.Путь,
                ВременныйКаталогВыгрузки.Путь,
                IConfigurationProperties.РежимОбработки.ПолучитьПоПоддержке)
            Консоль.Записать("В \"%{ПутьКФайламПроекта.Путь}\" добавлены измененые файлы из основной конфигурации\н")

            Консоль.Записать("\нDump:")
            Файлы.СоздатьКаталог("%{ВременныйКаталогВыгрузки.Путь}%{СР}Ext%{СР}ParentConfigurations")
            IIBOperation.ВыгрузитьКонфигурацию(
                "%{ВременныйКаталогВыгрузки.Путь}%{СР}Ext%{СР}ParentConfigurations%{СР}%{ФайлКонфигурацииПоставщика.Имя}",
                ВременныйКаталогБазыПоставщика.Путь,
                ПутьКФайламПлатформы,
                ИспользуетсяАвтономныйСервер,
                "",
                "",
                ПутьКФайламПроекта.Имя)

            Файлы.Удалить(ВременныйКаталогБазыПоставщика)
            Консоль.Записать("ИБ поставщика \"%{ВременныйКаталогБазыПоставщика.Путь}\" удалена\н")

            Файлы.Удалить(ПутьКФайламПроекта.Путь)
            Файлы.СоздатьКаталог(ПутьКФайламПроекта.Путь)
            Файлы.Скопировать(ВременныйКаталогВыгрузки.Путь, ПутьКФайламПроекта.Путь)
            Консоль.Записать("Перенесены изменения из \"%{ПутьКФайламПроекта.Путь}\" в основную конфигурацию\н")

            Файлы.Удалить(ВременныйКаталогВыгрузки)
            Консоль.Записать("Выгрузка поставщика \"%{ПутьКФайламПроекта.Путь}\" удалена\н")
        ;

        если СоздатьИнформационнуюБазу
            Консоль.Записать("\нCreateInfobase:")
            если КаталогВременнойБазы.Существует()
                Файлы.Удалить(КаталогВременнойБазы)
            ;
            IIBOperation.СоздатьИнформационнуюБазу(
                КаталогВременнойБазы.Путь,
                ПутьКФайламПлатформы,
                ИспользуетсяАвтономныйСервер)
        ;

        пер МаксимальноеЧислоПопыток  = ПутьКФайламПроекта.ЭтоРасширение ? 4 : 1
        пер ВременныйХешПроектаДо     = СтруктураКонфигурации.Хеш
        пер ВременныйХешПроектаПосле  = СтруктураКонфигурации.Хеш
        пер ВременныйХешПроектаЧетный = СтруктураКонфигурации.Хеш

        для ЧислоПопыток = 1 по МаксимальноеЧислоПопыток

            ВременныйХешПроектаДо = ВременныйХешПроектаПосле

            Консоль.Записать("\нLoadFromFiles:")
            IIBOperation.ЗагрузитьФайлыКонфигурации(
                ПутьКФайламПроекта.Путь,
                КаталогВременнойБазы.Путь,
                ПутьКФайламПлатформы,
                ИспользуетсяАвтономныйСервер,
                "",
                "",
                ПутьКФайламПроекта.Имя)

            Консоль.Записать("\нDumpFiles:")
            Файлы.Удалить(ПутьКФайламПроекта.Путь)
            Файлы.СоздатьКаталог(ПутьКФайламПроекта.Путь)

            IIBOperation.ВыгрузитьФайлыКонфигурации(
                ПутьКФайламПроекта.Путь,
                КаталогВременнойБазы.Путь,
                ПутьКФайламПлатформы,
                ИспользуетсяАвтономныйСервер,
                "",
                "",
                ПутьКФайламПроекта.Имя)

            знч ВремСтруктураКонфигурации = ПрочитатьСтруктуруКонфигурации(ПутьКФайламПроекта.Путь)
            ВременныйХешПроектаПосле = ВремСтруктураКонфигурации.Хеш

            если не ПутьКФайламПроекта.ЭтоРасширение или ВременныйХешПроектаПосле == ВременныйХешПроектаДо
                или ВременныйХешПроектаПосле == ВременныйХешПроектаЧетный
                // Вторая проврека нужна из-за ошибки выгрузки ролей в расширениях
                СтруктураКонфигурации = ВремСтруктураКонфигурации
                прервать
            иначе
                Консоль.Записать("\н*****************************************************************")
                Консоль.Записать("Повторная выгрузка репозитория")
                Консоль.Записать("*****************************************************************")
            ;

            если ЧислоПопыток % 2 == 0
                ВременныйХешПроектаЧетный = ВременныйХешПроектаПосле
            ;
        ;

        если ПутьКФайламПроекта.ЭтоРасширение
            и не ВременныйХешПроектаПосле == ВременныйХешПроектаДо
            и не ВременныйХешПроектаПосле == ВременныйХешПроектаЧетный
            выбросить новый ИсключениеНедопустимоеСостояние("Ошибка выгрузки проекта")
        ;

        Консоль.Записать("\нDump:")
        IIBOperation.ВыгрузитьКонфигурацию(
            ФайлСборки.Путь,
            КаталогВременнойБазы.Путь,
            ПутьКФайламПлатформы,
            ИспользуетсяАвтономныйСервер,
            "",
            "",
            ПутьКФайламПроекта.Имя)

        Результат = 1
        ПутьКФайламПроекта.Хеш = СтруктураКонфигурации.Хеш

    поймать Исключение: Исключение
        если не Исключение.Описание.Пусто()
            Консоль.Записать("\н#################################################################")
            Консоль.Записать(Исключение.Описание)
            Консоль.Записать("#################################################################")
        ;
        Результат = 2
    ;

    если ПараметрыПроекта.РежимСборкиПроекта == IProjectProperties.РежимыСборкиПроекта.ВПустойКонфигурации
        Файлы.Удалить(КаталогВременнойБазы)
        Консоль.Записать("Временная ИБ \"%{КаталогВременнойБазы.Путь}\" удалена\н")
    ;

    возврат Результат
;

@Глобально
метод ЗагрузитьКонфигурацию(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта, ПараметрыСоединенияИБ: IProjectProperties.ПараметрыСоединенияИБ, ЗагрузитьРасширения = Ложь)

    знч СР = Файлы.СимволРазделителя
    знч РасширениеФайлаЗагрузки = ЗагрузитьРасширения ? "cfe" : "cf"

    знч ФайлыЗагрузки = новый Массив<Файл>()
    если ЗагрузитьРасширения
        для Расширение из ПараметрыПроекта.Расширения
            ФайлыЗагрузки.Добавить(новый Файл(ПараметрыПроекта.ПутьКСборкеПроекта.Путь + СР + Расширение.Ключ + "." + РасширениеФайлаЗагрузки))
        ;
    иначе
        ФайлыЗагрузки.Добавить(новый Файл(ПараметрыПроекта.ПутьКСборкеПроекта.Путь + СР + "1Cv8." + РасширениеФайлаЗагрузки))
    ;

    для Файл из ФайлыЗагрузки
         если не Файл.Существует()
            Консоль.Записать(Файл.Путь)
            продолжить
        ;
        IIBOperation.ЗагрузитьКонфигурацию(
            Файл.Путь,
            ПараметрыСоединенияИБ.СтрокаСоединения,
            ПараметрыПроекта.ПутьКФайламПлатформы,
            ПараметрыПроекта.ИспользуетсяАвтономныйСервер,
            ПараметрыСоединенияИБ.Логин,
            ПараметрыСоединенияИБ.Пароль,
            (ЗагрузитьРасширения ? Файл.ИмяБезРасширения : "")
        )
    ;

;

@Глобально
метод ПодготовитьКонфигурацию(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта) : Булево

    пер СтатусВыполнения = Истина

    если ПараметрыПроекта.РежимСборкиПроекта == IProjectProperties.РежимыСборкиПроекта.ВПустойКонфигурации
        возврат СтатусВыполнения
    ;

    знч КаталогВременнойБазы = ПолучитьВременныйКаталог(ПараметрыПроекта, IProjectProperties.ИМЯ_КАТАЛОГА_КОНФИГУРАЦИИ)

    если КаталогВременнойБазы.Дочерние.Размер() == 0
        СтатусВыполнения = СобратьФайлыКонфигурации(ПараметрыПроекта)
    иначе
        знч РезультатСборки = СобратьПатчКонфигурации(ПараметрыПроекта)
        если РезультатСборки == 0
            знч ПараметрыСоединенияИБ = новый IProjectProperties.ПараметрыСоединенияИБ(
                КаталогВременнойБазы.Путь)
            ЗагрузитьПатчКонфигурации(ПараметрыПроекта, ПараметрыСоединенияИБ)
        ;
        СтатусВыполнения = РезультатСборки < 2
    ;

    возврат СтатусВыполнения

;

@Глобально
метод ЗагрузитьПатчКонфигурации(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта, ПараметрыСоединенияИБ: IProjectProperties.ПараметрыСоединенияИБ, ПолучитьКонфигурациюПоставщика = Ложь, ПолучитьФайлСравнения = Ложь)

    знч СР = Файлы.СимволРазделителя

    знч КаталогДляЗагрузки  = новый Файл("%{ПараметрыПроекта.ПутьКВременнымФайлам.Путь}%{СР}%{IProjectProperties.ИМЯ_КАТАЛОГА_ПАТЧА}")
    знч ФайлДляЗагрузки     = новый Файл("%{ПараметрыПроекта.ПутьКСборкеПроекта.Путь}%{СР}%{IProjectProperties.ИМЯ_КАТАЛОГА_ПАТЧА}.zip")
    знч ФайлЗагрузки        = новый Файл("%{ФайлДляЗагрузки.Путь.Заменить(".zip", "_in")}")
    знч ФайлВыгрузки        = новый Файл("%{ФайлДляЗагрузки.Путь.Заменить(".zip", "_out")}")

    если не КаталогДляЗагрузки.Существует()
        или не ФайлЗагрузки.Существует()
        или не ФайлВыгрузки.Существует()
        возврат
    ;

    знч КаталогДляВыгрузки = ПолучитьВременныйКаталог(ПараметрыПроекта, IProjectProperties.ИМЯ_КАТАЛОГА_ПАТЧА)

    пер ПутьКФайлуПоставщика = IConfiguration.ПолучитьФайлПоставщика(ПараметрыПроекта.ПутьКФайламКонфигурации.Путь)
    пер ПутьКФайлуПоставщикаЗагрузки = ""

    если (ПутьКФайлуПоставщика.Путь.Пусто()
        или ПолучитьКонфигурациюПоставщика)
        IIBOperation.ВыгрузитьФайлыКонфигурации(
            КаталогДляВыгрузки.Путь,
            ПараметрыСоединенияИБ.СтрокаСоединения,
            ПараметрыПроекта.ПутьКФайламПлатформы,
            ПараметрыПроекта.ИспользуетсяАвтономныйСервер,
            ПараметрыСоединенияИБ.Логин,
            ПараметрыСоединенияИБ.Пароль,
            "",
            ФайлВыгрузки)
        ПутьКФайлуПоставщика = IConfiguration.ПолучитьФайлПоставщика(КаталогДляВыгрузки.Путь)
        ПутьКФайлуПоставщикаЗагрузки = ПутьКФайлуПоставщика.Путь.Заменить(КаталогДляВыгрузки.Путь, КаталогДляЗагрузки.Путь)
    иначе
        ПутьКФайлуПоставщикаЗагрузки = ПутьКФайлуПоставщика.Путь.Заменить(ПараметрыПроекта.ПутьКФайламКонфигурации.Путь, КаталогДляЗагрузки.Путь + СР)
    ;
    Файлы.Скопировать(ПутьКФайлуПоставщика, ПутьКФайлуПоставщикаЗагрузки)

    Консоль.Записать("\нLoadFromFiles:")
    IIBOperation.ЗагрузитьФайлыКонфигурации(
        //(ФайлДляЗагрузки.Существует() ? ФайлДляЗагрузки.Путь : КаталогДляЗагрузки.Путь),
        КаталогДляЗагрузки.Путь,
        ПараметрыСоединенияИБ.СтрокаСоединения,
        ПараметрыПроекта.ПутьКФайламПлатформы,
        ПараметрыПроекта.ИспользуетсяАвтономныйСервер,
        ПараметрыСоединенияИБ.Логин,
        ПараметрыСоединенияИБ.Пароль,
        "",
        ФайлЗагрузки)

    если ПараметрыПроекта.ФайлВерсийКонфигурации.Существует()
        знч ФайлВерсийКонфигурации = IConfiguration.ПолучитьФайлConfigDumpInfo(ПараметрыПроекта.ПутьКФайламКонфигурации.Путь)
        знч НастройкиКопирования = новый НастройкиКопированияФайлов()
            .ПропускатьСуществующие(Ложь)
        Файлы.Скопировать(ФайлВерсийКонфигурации, ПараметрыПроекта.ФайлВерсийКонфигурации, НастройкиКопирования)
    ;

    если ПолучитьФайлСравнения
        Консоль.Записать("\нCompareCfg:")
        IIBOperation.СравнитьКонфигурации(
            ПараметрыПроекта.ПутьКВременнымФайлам.Путь + СР + "ИзмененияВКонфигурации.mxl",
            ПараметрыСоединенияИБ.СтрокаСоединения,
            ПараметрыПроекта.ПутьКФайламПлатформы,
            ПараметрыСоединенияИБ.Логин,
            ПараметрыСоединенияИБ.Пароль)
    ;

    если не ПараметрыПроекта.ФайлВерсийКонфигурации == Неопределено
        знч ConfigDumpInfoВыгрузки = IConfiguration.ПолучитьФайлConfigDumpInfo(КаталогДляВыгрузки.Путь)
        Файлы.Скопировать(ConfigDumpInfoВыгрузки, ПараметрыПроекта.ФайлВерсийКонфигурации)
    ;

    Файлы.Удалить(КаталогДляВыгрузки, Истина)
    Файлы.Удалить(ПутьКФайлуПоставщикаЗагрузки, Истина)

;

@Глобально
метод ПеревестиФайлИзмененныхОбъектовВНовыйФормат(ПутьКФайламПроекта: Строка)

    IConfiguration.ПеревестиВНовыйФорматParentConfigurations(ПутьКФайламПроекта)
;

@Глобально
метод ПеревестиФайлИсторииОбъектовВНовыйФормат(ПутьКФайламПроекта: Строка)

    IConfiguration.ПеревестиВНовыйФорматConfigDumpInfo(ПутьКФайламПроекта)
;

@Глобально
метод АнализироватьНастройкиПоддержки(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта, ИсправитьОшибки = Ложь, ДляКонфигурацииПоставщика = Ложь) : Булево

    пер ПутьКФайламПроекта = ПараметрыПроекта.ПутьКФайламКонфигурации

    если ДляКонфигурацииПоставщика
        ПолучитьФайлыКонфигурации(ПараметрыПроекта, ДляКонфигурацииПоставщика)
        ПутьКФайламПроекта = ПараметрыПроекта.ПутьКФайламКонфигурацииПоставщика
    ;

    знч СтуркутураКонфигурации = IConfiguration.ПрочитатьСтруктуруКонфигурации(ПутьКФайламПроекта.Путь, IConfigurationProperties.РежимОбработки.Основной)
    IConfiguration.ИсправитьНастройкиПоддержки(СтуркутураКонфигурации, ИсправитьОшибки)

    возврат Истина
;

@Глобально
метод ПолучитьФайлыКонфигурации(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта, ДляКонфигурацииПоставщика = Ложь, Режим = 0) : Булево

    пер ПутьКФайламПроекта = ПараметрыПроекта.ПутьКФайламКонфигурации
    если ДляКонфигурацииПоставщика
        ПутьКФайламПроекта = ПараметрыПроекта.ПутьКФайламКонфигурацииПоставщика
    ;
    знч КаталогФайловКонфигурации = новый Файл(ПутьКФайламПроекта.Путь)
    пер ФайлыДляВыгрузки = ""

    если Режим == 2
        ФайлыДляВыгрузки = "ConfigDumpInfo.xml"
    ;

    если ДляКонфигурацииПоставщика

        знч ФайлConfigDumpInfo = IConfiguration.ПолучитьФайлConfigDumpInfo(ПутьКФайламПроекта.Путь)
        если Режим == 2 и ФайлConfigDumpInfo.Существует()
            возврат Истина
        ;

        знч СтруктураКонфигурации = ПрочитатьСтруктуруКонфигурации(ПутьКФайламПроекта.Путь)
        если СтруктураКонфигурации.Хеш == ПутьКФайламПроекта.Хеш и КаталогФайловКонфигурации.Существует()
            возврат Истина
        ;

        если КаталогФайловКонфигурации.Существует()
            Файлы.Удалить(КаталогФайловКонфигурации)
        ;

        знч КаталогВременнойБазыПоставщика = ПолучитьВременныйКаталог(ПараметрыПроекта, IProjectProperties.ИМЯ_КАТАЛОГА_КОНФИГУРАЦИИ_ПОСТАВЩИКА)
        знч ПутьКФайлуПоставщика = IConfiguration.ПолучитьФайлПоставщика(ПараметрыПроекта.ПутьКФайламКонфигурации.Путь)

        Консоль.Записать("\нCreateInfobase:")
        IIBOperation.СоздатьИнформационнуюБазу(
            КаталогВременнойБазыПоставщика.Путь,
            ПараметрыПроекта.ПутьКФайламПлатформы,
            ПараметрыПроекта.ИспользуетсяАвтономныйСервер)

        Консоль.Записать("\нLoadFromFile:")
        IIBOperation.ЗагрузитьКонфигурацию(
            ПутьКФайлуПоставщика.Путь,
            КаталогВременнойБазыПоставщика.Путь,
            ПараметрыПроекта.ПутьКФайламПлатформы,
            ПараметрыПроекта.ИспользуетсяАвтономныйСервер,
            "",
            "",
            "")

        Консоль.Записать("\нDumpFiles:")
        IIBOperation.ВыгрузитьФайлыКонфигурации(
            ПутьКФайламПроекта.Путь,
            КаталогВременнойБазыПоставщика.Путь,
            ПараметрыПроекта.ПутьКФайламПлатформы,
            ПараметрыПроекта.ИспользуетсяАвтономныйСервер,
            "",
            "",
            "",
            ФайлыДляВыгрузки)

        ПутьКФайламПроекта.Хеш = СтруктураКонфигурации.Хеш

        Файлы.Удалить(КаталогВременнойБазыПоставщика)
        Консоль.Записать("ИБ поставщика удалена\н")

    иначе

        если ПараметрыПроекта.ПараметрыСоединенияИБ.СтрокаСоединения.Пусто()
          Консоль.Записать("Не заполнен путь к ИБ в файле хэша ProjectsHash.json")
          возврат Ложь
        ;

        знч ПутьКФайлуВерсии = (Режим == 2 или ПараметрыПроекта.ФайлВерсийКонфигурации == Неопределено) ? "" :
            ПараметрыПроекта.ФайлВерсийКонфигурации.Путь

        пер ПутьДляВыгрузки = ПутьКФайламПроекта.Путь
        если Режим == 2
            Консоль.Записать("\нDumpFiles(Config dump info):")
            ПутьДляВыгрузки = ПараметрыПроекта.ПутьКВременнымФайлам.Путь
        ;
        если не ПутьКФайлуВерсии.Пусто()
            ПутьДляВыгрузки = ПутьКФайлуВерсии.Заменить(".xml", "")
        ;

        IIBOperation.ВыгрузитьФайлыКонфигурации(
            ПутьДляВыгрузки,
            ПараметрыПроекта.ПараметрыСоединенияИБ.СтрокаСоединения,
            ПараметрыПроекта.ПутьКФайламПлатформы,
            ПараметрыПроекта.ИспользуетсяАвтономныйСервер,
            ПараметрыПроекта.ПараметрыСоединенияИБ.Логин,
            ПараметрыПроекта.ПараметрыСоединенияИБ.Пароль,
            "",
            ФайлыДляВыгрузки,
            ПутьКФайлуВерсии)

        если не ПутьКФайлуВерсии.Пусто()
            знч ConfigDumpInfoВыгрузки = IConfiguration.ПолучитьФайлConfigDumpInfo(ПутьДляВыгрузки)
            Файлы.Скопировать(ConfigDumpInfoВыгрузки, ПараметрыПроекта.ФайлВерсийКонфигурации)
            знч НастройкиПоиска = новый НастройкиПоискаФайлов()
                .МаксимальнаяГлубина(1)
            знч НайденныеФайлы = Файлы.Найти(ПутьДляВыгрузки, НастройкиПоиска)
            для ФайлИсточник из НайденныеФайлы
                знч ФайлЦель = новый Файл(ПутьКФайламПроекта.Путь + ФайлИсточник.Путь.Заменить(ПутьДляВыгрузки, ""))
                если ФайлЦель.Существует()
                    Файлы.Удалить(ФайлЦель)
                ;
                Файлы.Переместить(ФайлИсточник, ФайлЦель)
            ;
        ;
    ;

    если Режим != 2
        ПеревестиФайлИзмененныхОбъектовВНовыйФормат(ПутьКФайламПроекта.Путь)
        ПеревестиФайлИсторииОбъектовВНовыйФормат(ПутьКФайламПроекта.Путь)
    ;

    возврат Истина
;

@Глобально
метод ПрочитатьСтруктуруКонфигурации(ПутьККаталогуПроекта: Строка, ПутьККаталогуСборкиПатча = "", ИсправитьОшибки = Ложь) : IConfigurationProperties.СтуркутураКонфигурации

    знч РежимОбработки = IConfigurationProperties.РежимОбработки.ПолучитьПоПоддержке
    знч СтуркутураКонфигурации = IConfiguration.ПрочитатьСтруктуруКонфигурации(
        ПутьККаталогуПроекта,
        РежимОбработки)
    IConfiguration.ИсправитьНастройкиПоддержки(СтуркутураКонфигурации, ИсправитьОшибки)
    IConfiguration.ОбработатьФайлыКонфигурации(
        СтуркутураКонфигурации,
        ПутьККаталогуПроекта,
        ПутьККаталогуСборкиПатча,
        РежимОбработки)

    возврат СтуркутураКонфигурации

;

@Локально
метод ВывестиИнформациюПоСобраннымПроектам(СобранныеПроекты: Массив<Строка>)

    если не СобранныеПроекты.Пусто()
        Консоль.Записать("Собраны проекты:")
    ;

    для ИмяПроекта из СобранныеПроекты
        Консоль.Записать(ИмяПроекта)
    ;

;

@Локально
метод ПолучитьВременныйКаталог(ПараметрыПроекта: IProjectProperties.ПараметрыПроекта, ИмяКаталога: Строка) : Файл

    знч СР = Файлы.СимволРазделителя
    пер ПутьКВременномуКаталогу = ""
    если ПараметрыПроекта.ПутьКВременнымФайлам.ОтносительныйПуть.Пусто()
        ПутьКВременномуКаталогу = Файлы.СоздатьВременныйКаталог().Путь
    иначе
        ПутьКВременномуКаталогу = ПараметрыПроекта.ПутьКВременнымФайлам.ОтносительныйПуть + СР + ИмяКаталога
    ;

    знч ВременныйКаталог = новый Файл(ПутьКВременномуКаталогу)

    возврат ВременныйКаталог
;
